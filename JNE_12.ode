# JNE_12.ode
#
# This XPPAUT files contains the Plant model for parabolic bursting. See
# http://www.scholarpedia.org/article/Plant_model for details. It was
# used to make Fig. 2 in the paper "Two Types of Burst Firing
# in Gonadotrophin-Releasing Hormone Neurones", Z. Chu, M. Tomaiuolo, 
# R. Bertram, S. M. Moenter, Journal of Neuroendocrinology, 24:1065-1077, 2012.

# initial conditions

# Variables:
#    v -- membrane potential
#    h -- inactivation of an inward current
#    n -- activation of an ooutward potassium current
#    x -- slow inward current activation
#    ca -- free cytosolic calcium concentration

v(0)=-50
h(0)=0.1
n(0)=0.1
x(0)=0.1
ca(0)=0.1

# parameters

par cm=1
par gi=4
par gt=0.01
par gk=0.3
par gkca=0.03
par gleak=0.003
par vi=30
par vk=-75
par vleak=-40
par vca=140

par rho=0.00015
par kc=0.00425
par taux=9400

par noise=0

# functions

wiener w

vs=(v+65.07)*1.20952

alpham=0.1*(50-vs)/(exp((50-vs)/10)-1)
betam=4*exp((25-vs)/18)

alphah=0.07*exp((25-vs)/20)
betah=1/(exp((55-vs)/10)+1)

alphan=0.01*(55-vs)/(exp((55-vs)/10)-1)
betan=0.125*exp((45-vs)/80)

hinf=alphah/(alphah+betah)
minf=alpham/(alpham+betam)
ninf=alphan/(alphan+betan)

xinf=1/(exp(-0.3*(v+40))+1)

tauh=12.5/(alphah+betah)
taun=12.5/(alphan+betan)

Ihi=gi*minf^3*h*(v-vi)
Ixi=gt*x*(v-vi)
Ik=gk*n^4*(v-vk)
Ikca=gkca*(ca/(0.5+ca))*(v-vk)
Ileak=gleak*(v-vleak)
Iapp=0
Inoise=noise*w

# equations

v'=-1/cm*(Ihi + Ixi + Ik + Ikca + Ileak - Iapp - Inoise)
h'=(hinf-h)/tauh
n'=(ninf-n)/taun
x'=(xinf-x)/taux
ca'=rho*(kc*x*(vca-v)-ca)


@ method=E,dt=0.1,total=2000000,maxstor=10000000,noutput=10
@ xp=t, yp=v, xlo=0, xhi=800000, ylo=-70, yhi=40

set ppb_rep1 {noise=0.06}
set ppb_rep2 {noise=0.07}
set ppb_rep3 {noise=0.08}
set ppb_rep4 {noise=0.09}
set ppb_rep5 {noise=0.1}

done
