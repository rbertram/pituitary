# Figs1-3.ode
#
# This XPPAUT file contains the mathematical model used in Figures 1-3 of
# Bertram, Egli, Toporikova, and Freeman, American Journal of Physiology,
# in press, 2006.
#
# Variables (dimensionless): p = prolactin concentration
#                            d = dopamine concentration
#                            vip = VIP neuron activity
#

p(0)=0
d(0)=20

# Actions

" {vipdur=0} Free-running

# units of t are hr

# prolactin parameters
par Tp=6, kd=1, q=0.5

# dopamine parameters
par Td=10, kp=0.03, rv=2, tau=3

# Circadian VIP neuron activity 
par period=24, tf1=2, vipdur=3
vip = heav(mod(t,period)-tf1)-heav(mod(t,period)-(tf1+vipdur))

# OT injection
par tOT=24
OTswitch = heav(t-tOT)

# Equations

p' = Tp*(1/(kd+d^2))-q*p 
d' = Td*(1+kp*delay(p,tau)^2)-q*d-rv*vip*d-Td*OTswitch

aux tdays=t/24
aux Vip=vip

# set max delay and other xpp parameters
@ delay=6, dt=0.2, total=378, maxstor=20000
@ bounds=10000000, xp=tdays, yp=p
@ xlo=0, xhi=15, ylo=0, yhi=10

done
