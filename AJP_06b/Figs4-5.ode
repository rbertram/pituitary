# Figs4-5.ode

# This XPPAUT file contains the mathematical model used in Figures 4-5 of
# Bertram, Egli, Toporikova, and Freeman, American Journal of Physiology,
# in press, 2006. It includes stimulatory action of oxytocin directly
# on lactotrophs.

# Figure 4: no stimulatory factor, so otact=0
# Figure 5: with (sustained) stimulatory factor, so otact=2

# Variables (dimensionless): p = prolactin concentration
#                            d = dopamine concentration
#                            o = oxytocin concentration
#                            vip = VIP neuron activity

p(0)=0
d(0)=20
o(0)=0
N(0)=0

# Actions

" {OTact=2} stimulatory factor

# Note: N is interpreted here as a switch that is internal to the lactotrophs. 
# When it is turned on by OT injection it directly activates the lactos, but
# has no effect on the DA neurons.

# units of t are hr

# prolactin parameters
par vo=0.01, Tp=6, q=0.5, kd=1, otact=0

# dopamine parameters
par Td=10, kp=0.03, rv=2, tau=3

# Circadian VIP neuron activity
par period=24, tf1=2, vipdur=3
vip = heav(mod(t,period)-tf1)-heav(mod(t,period)-(tf1+vipdur))

# Lactotroph switch parameters
par von=0.0002, rn=0.2

# Externally applied OT pulse (set p_inj=1000 for OT injection)
par tinj=48, p_inj=1000, tpdur=2
I = p_inj*(heav(t-tinj)-heav(t-tinj-tpdur))

# Equations

p' = Tp*(1/(kd+d^2))-q*p+vo*o+otact*N 
d' = Td*1+Td*kp*delay(p,tau)^2-q*d-rv*vip*d
o' = I-q*o 
N' = von*o-rn*N*(N-.5)*(N-1) 

aux tdays=t/24
aux Vip=vip

# set max delay and other xpp parameters
@ delay=6, dt=0.2, total=378, maxstor=20000
@ bounds=10000000, xp=tdays, yp=p, bell=off
@ xlo=0, xhi=15, ylo=0, yhi=10

done
