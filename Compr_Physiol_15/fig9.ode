# Hodgkin-Huxley equations with type 2 dynamics.
# Includes noise
# Used to make Fig. 9 in Mathematical Modeling in Neuroendocrinology, by
# R. Bertram, Comprehensive Physiology, 5:911-927, 2015.
 
# initial conditions

init v=-63, n=0.35, m=0.07, h=0.64

# noise added to monostable system: iap=1, noise=0.3
# noise added to bistable system: iap=1.3, noise=0.3
# noise added to bistable system: iap=1.5, noise=0.3

# parameters
par noise=0.3
par iap=1.0
num vna=50,vk=-75,vl=-50,gna=120,gk=36,gl=0.3,c=1

wiener w

# kinetic functions
am =  .1*(v+40)/(1-exp(-(v+40)/10))
bm =  4*exp(-(v+65)/18)
ah =  .07*exp(-(v+65)/20)
bh =  1/(1+exp(-(v+30)/10))
an =  .01*(v+55)/(1-exp(-(v+55)/10))
bn =  .125*exp(-(v+65)/80)

# infinity and time constant functions
minf = am/(am+bm)
hinf = ah/(ah+bh)
ninf = an/(an+bn)
tn   = 1/(an+bn)
th   = 1/(ah+bh)
tm   = 1/(am+bm)

inoise = noise*w

v'=(iap - gna*h*(v-vna)*m^3-gk*(v-vk)*n^4-gl*(v-vl)-inoise)/c
h'=-(h-hinf)/th
n'=-(n-ninf)/tn
m'=-(m-minf)/tm

# XPP parameters
@ maxstor=120000
@ total=1100, dt=0.05, bounds=1000000
@ method=Euler, bell=off
@ xlo=0, xhi=1100, ylo=-80, yhi=40
# AUTO parameters
@ Nmax=500, NPr=200, ParMin=-50, ParMax=300
@ AutoXMin=-5, AutoXMax=190
@ AutoYMin=-80, AutoYMax=35
done

