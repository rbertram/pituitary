# Endo_07.ode
#
# Computer code used in the article "Oxytocin Action at the 
# Lactotroph is Required for Prolactin Surges in Cervically Stimulated
# Ovariectomized Rats" by D. T. McKee, M. O. Poletini, R. Bertram, and
# M. E. Freeman, Endocrinology, 148:4649--4657, 2007. 

# Variables (dimensionless): p = prolactin concentration
#                            d = dopamine concentration
#                            ocs = oxytocin concentration resulting from CS
#                            opvn = oxytocin concentration from PVN
#                            op = oxytocin concentration in pituitary
#                            vip = VIP neuron activity
#                            N = interneuron activity

p(0)=0
d(0)=20
ocs(0)=0
opvn(0)=0
N(0)=0

# units of t are hr

# prolactin parameters
par vop=1.0,Tp=1, q=0.5, kd=1

# dopamine parameters
par Td=10, kp=0.3, rv=2, tau=3

# OT-sensing neuron parameters
par vcs=0.0002, rn=0.2

# Cervical stimulation (causing OT pulse) 
par tcs=48, p_cs=1000, tpdur=2
I = p_cs*(heav(t-tcs)-heav(t-tcs-tpdur))

# Circadian VIP neuron activity
par period=24, tf1=2, vipdur=3
vip = heav(mod(t,period)-tf1)-heav(mod(t,period)-(tf1+vipdur))

# OT from OT neurons of the PVN to the pituitary
par O_pit=3, ko=1
To = O_pit*heav(t-tcs)

# Total OT in pituitary
op = Opvn + ocs 

# OT antagonist (OTA) application
par tOTA=24, tOTAdur=96
OTA = (heav(t-tOTA)-heav(t-tOTA-tOTAdur))
vo = vop*(1-OTA)

# Equations
p' = (Tp+vo*op)*(1/(kd+d^2))-q*p
d' = Td*(1-N)+kp*delay(p,tau)^2-q*d-rv*vip*d
ocs' = I-q*ocs 
opvn' = To/(1+ko*p^2)-q*opvn 
N' = vcs*ocs-rn*N*(N-.5)*(N-1) 

aux tdays=t/24
aux Vip=vip
aux Op=op
aux OTA=OTA

# set max delay and other xpp parameters
@ delay=6, dt=0.2, total=425, maxstor=20000
@ bounds=10000000, xp=tdays, yp=p, bell=off
@ xlo=0, xhi=16, ylo=0, yhi=10

done
